/* --- START OF: common sniplet for build.gradle.kts in repo root --- */
plugins {
    `java-library`
    `maven-publish`
}

// --- Dependency resolution repos (read) ---
allprojects {
    layout.buildDirectory.set(
        rootProject.layout.projectDirectory.dir("run/bld/gradle/${project.name}")
    )
}

// Simple sanity task
tasks.register("ciHelp") {
    doLast { println("Algites root Gradle build detected.") }
}

allprojects {
    tasks.withType<Test>().configureEach {
        useTestNG()
    }
}

publishing {
    publications {
        create<MavenPublication>("mavenJava") {
            from(components["java"])
        }
    }
}

fun String.capFirst(): String =
    replaceFirstChar { if (it.isLowerCase()) it.titlecase() else it.toString() }

val locVisibility = providers.gradleProperty("ALGITES_VISIBILITY").orNull ?: "pub"
val locDirection  = providers.gradleProperty("ALGITES_DIRECTION").orNull ?: "upload"

val locRepoUrl  = providers.gradleProperty("ALGITES_REPO_URL").orNull
val locRepoUser = providers.gradleProperty("ALGITES_REPO_USER").orNull
val locRepoPass = providers.gradleProperty("ALGITES_REPO_PASS").orNull

val locHasRemoteRepo = (locRepoUrl != null && locRepoUser != null && locRepoPass != null)

/* Internal Gradle repo name (stable, camelCase) */
val locRepoName = buildString {
    append("algites")
    append(locVisibility.capFirst())
    append(locDirection.capFirst())
}

val locIsCi = providers.environmentVariable("CI").orNull == "true"
if (locIsCi && !locHasRemoteRepo) {
    throw GradleException("CI build requires ALGITES_REPO_* for publishing.")
}


subprojects {
    /* artifactId = "<rootProject.name>_<subproject-path-with-dots>" */
    val locRepoName = rootProject.name

    val locPathDots = project.path
        .removePrefix(":")
        .replace(':', '.')

    val locCanonicalArtifactId = if (locPathDots.isBlank()) {
        locRepoName
    } else {
        "${locRepoName}_${locPathDots}"
    }

    /* Set local archive base name (jar file name prefix) */
    plugins.withId("base") {
        base {
            archivesName.set(locCanonicalArtifactId)
        }
    }

    /* Set Maven artifactId for all Maven publications in this subproject */
    plugins.withId("maven-publish") {
        publishing {
            publications.withType(MavenPublication::class.java).configureEach {
                artifactId = locCanonicalArtifactId
            }
        }
        extensions.configure<PublishingExtension>("publishing") {
            repositories {
                if (locHasRemoteRepo) {
                    maven {
                        name = locRepoName
                        url = uri(locRepoUrl!!)
                        credentials {
                            username = locRepoUser!!
                            password = locRepoPass!!
                        }
                    }
                }
            }
        }
    }
    /* Local fallback: if NOT CI and no remote repo, publish => publishToMavenLocal */
    tasks.matching { it.name == "publish" }.configureEach {
        if (!locIsCi && !locHasRemoteRepo) {
            dependsOn("publishToMavenLocal")
        }
    }
}

/* Disable publishing tasks in root completely */
if (project == rootProject) {
    tasks.withType<PublishToMavenRepository>().configureEach { enabled = false }
    tasks.matching { it.name == "publish" || it.name == "publishToMavenLocal" }.configureEach { enabled = false }
}

allprojects {
    configurations.configureEach {
        resolutionStrategy.dependencySubstitution {
            all {
                val locRequested = requested
                if (locRequested is org.gradle.api.artifacts.component.ModuleComponentSelector) {

                    val locRootName = rootProject.name
                    val locModule = locRequested.module
                    val locGroup = locRequested.group

                    val locExpectedPrefix = "${locRootName}_"
                    val locMatchesRepo = (locModule == locRootName || locModule.startsWith(locExpectedPrefix))
                    val locMatchesGroup = (locGroup == project.group.toString())

                    if (locMatchesRepo && locMatchesGroup) {
                        val locPathDots = if (locModule == locRootName) "" else locModule.removePrefix(locExpectedPrefix)
                        val locProjectPath = if (locPathDots.isBlank()) ":" else ":" + locPathDots.replace('.', ':')

                        val locTargetProject = rootProject.findProject(locProjectPath)
                        if (locTargetProject != null) {
                            useTarget(locTargetProject)
                        }
                    }
                }
            }
        }
    }
}

/* --- END OF: common sniplet for build.gradle.kts in repo root --- */

allprojects {
    group = "eu.algites.tool"
    version = "0.0.1-SNAPSHOT"
}